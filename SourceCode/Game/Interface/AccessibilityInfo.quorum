package Templates.SkyHopper.Interface

use Libraries.Interface.Item2D
use Libraries.Compute.Vector2

class AccessibilityInfo is Item2D
    
    on create
        SetFocusable(true)
        SetAccessibilityCode(parent:Item:CUSTOM)
    end
    
    action Set(Player player, Item2D target)
        if target = undefined
            SetName(player:GetName())
            
            text description = "at " + cast(integer, player:GetGlobalX()) + ", " + cast(integer, player:GetGlobalY()) + ". "
            description = description + "Use the tab keys to find elements of interest. Press Escape at any time to hear this information again."
            SetDescription(description)
            
            SetPosition(player:GetGlobalX(), player:GetGlobalY())
            SetSize(player:GetWidth(), player:GetHeight())
        else
            SetName(target:GetName())
            
            text description = ""
            
            Vector2 vector
            vector:Set(target:GetCenterX(), target:GetCenterY())
            description = cast(integer, vector:Distance(player:GetCenterX(), player:GetCenterY())) + " units "
            
            vector:Subtract(player:GetCenterX(), player:GetCenterY())
            number angle = vector:Angle()
            // Ensure the angle is between 0 to 360.
            repeat while angle < 0
                angle = angle + 360
            end
            repeat while angle > 360
                angle = angle - 360
            end
            
            text direction = ""
            if angle >= 337.5 or angle <= 22.5
                direction = "right"
            elseif angle >= 22.5 and angle <= 67.5
                direction = "up and right"
            elseif angle >= 67.5 and angle <= 112.5
                direction = "up"
            elseif angle >= 112.5 and angle <= 157.5
                direction = "up and left"
            elseif angle >= 157.5 and angle <= 202.5
                direction = "left"
            elseif angle >= 202.5 and angle <= 247.5
                direction = "down and left"
            elseif angle >= 247.5 and angle <= 292.5
                direction = "down"
            elseif angle >= 292.5 and angle <= 337.5
                direction = "down and right"
            end
            
            description = description + direction + ", " + cast(integer, angle) + " degrees. "
            description = description + target:GetDescription()
            SetDescription(description)
            
            SetPosition(target:GetGlobalX(), target:GetGlobalY())
            SetSize(target:GetWidth(), target:GetHeight())
        end
    end
    
end